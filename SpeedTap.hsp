/* 〜変数紹介(載ってない変数はそれっぽく察してね)〜
 * mmlist: 音声ファイルの存在確認リスト(mmload_opt と mmplay_opt で使用)
 * posx: 画像のx座標移動(反動時逆転)
 * posy: 画像のy座標移動(反動時逆転)
 * a: 画像を動かす大きさ(絶対値)
 * boxx: 画像を表示するX座標
 * boxy: 画像を表示するY座標
 * clk: タップの認識
 * stg: 的のタップ回数(stage)
 * time: プレイ時間／残り時間
 * lv: 現在のレベル
 * besttime: タイムアタック最高記録(セーブデータにも使用)
 * beststg: カウントダウン最高記録(セーブデータにも使用)
 * mode: ゲームモード(0=Time Attack, 1=Countdown)
 */
#include "hsp3dish.as"

	info_os=sysinfo(0)
	if instr(info_os,0,"iOS")=-1 & instr(info_os,0,"android")=-1 & instr(info_os,0,"Emscripten")=-1 : assets_dir="data/" : else : assets_dir=""

	dim mmlist,8
	mmload_opt assets_dir+"title.mp3",0,1
	mmload_opt assets_dir+"bgm.mp3",1,1
	mmload_opt assets_dir+"result.mp3",2,1
	mmload_opt assets_dir+"ok.wav",3,0
	mmload_opt assets_dir+"countdown.wav",4,0
	mmload_opt assets_dir+"pause.wav",5,0
	mmload_opt assets_dir+"config.mp3",6,1
	mmload_opt assets_dir+"go.wav",7,0

	celload assets_dir+"target.png",1
	celdiv 1,200,200
	//
	buffer 2
	pos 0,0
	picload assets_dir+"timeattack.png",1
	pos 0,0
	//
	buffer 3
	pos 0,0
	picload assets_dir+"countdown.png",1
	pos 0,0
	//
	buffer 4
	pos 0,0
	picload assets_dir+"reset.png",1
	pos 0,0
	//
	buffer 5
	pos 0,0
	picload assets_dir+"deltatk.png",1
	//
	buffer 6
	pos 0,0
	picload assets_dir+"delctdn.png",1
	//
	buffer 7
	pos 0,0
	picload assets_dir+"delback.png",1
	//
	buffer 8
	pos 0,0
	picload assets_dir+"pause.png",1
	//
	buffer 9
	pos 0,0
	picload assets_dir+"continue.png",1
	pos 0,0
	//
	buffer 10
	pos 0,0
	picload assets_dir+"totitle.png",1
	pos 0,0
	//
	buffer 11
	pos 0,0
	picload assets_dir+"save.png",1
	pos 0,0
	//
	gsel 0
	randomize
*top
	mmstop
	clrobj
	lv=1 : posx=0 : posy=0 : a=0 : time=0 : stg=-1 : prev_ok=-32768
	exist "save.bin"
	if strsize!-1 {
		bload "save.bin",besttime,4
		bload "save.bin",beststg,4,4
	} else {
		besttime=-1
		beststg=-1
	}

	mmplay_opt 0
	
	objsize 512,128
	objimage 2,0,0
	pos 1280/2-512,235
	button "",*timeattack
	objimage 3,0,0
	pos 1280/2,235
	button "",*countdown

	objsize 128,128
	objimage 4,0,0
	pos 1280-128,720-128
	button "",*options

	title "早押しゲーム"
	repeat
		redraw 0
		color 0,0,0 : boxf
		font "",99,1
		pos 340,50 : color 255,255,0
		print "早押しゲーム"
		pos 200,400 : font "",30 : color 0,255,0
		if besttime=-1 : mes "タイムアタック記録: なし" : else : mes "タイムアタック記録: "+besttime+" 秒"
		if beststg=-1 : mes "カウントダウン記録: なし" : else : mes "カウントダウン記録: "+beststg+" 回"
		mes "\nタイムアタック: 的を50回タップする速度を競います。"
		mes "カウントダウン: 20秒以内に的をタップした回数を競います。"
		mes "10回タップごとに難易度が最大5まで上がります。"
		redraw 1
		await 16
	loop
	stop

*timeattack
	mode=0
	goto *prepare

*countdown
	mode=1
	goto *prepare

*prepare
	mmstop
	clrobj
	font "",128,1
	repeat
		if cnt>=3 : break
		mmplay_opt 4
		redraw 0
		color 0,0,0
		boxf
		pos 600,280
		color 255,255,255
		print 3-cnt
		redraw 1
		await 1000
	loop
	mmplay_opt 7
	mmplay_opt 1
	pos 1280-256,720-128
	objimage 8,0,0
	objsize 256,128
	button gosub "",*pause
	gosub *nextstage
*main
	repeat
		title "早押しゲーム "+stg+"回 "+time+"秒 Lv."+lv
		redraw 0
		
		//背景
		color 0,0,0
		boxf
		
		//あたり判定、画像配置
		gosub *move
		pos boxx,boxy
		celput 1
		if cnt<prev_ok+8 : pos prev_boxx,prev_boxy : celput 1,1
		stick clk,,0
		if clk&256 {
			if mousex>=boxx & mousex<=boxx+200 & mousey>=boxy & mousey<=boxy+200 {
				mmplay_opt 3
				prev_boxx=boxx
				prev_boxy=boxy
				prev_ok=cnt
				gosub *nextstage
				if cleared : break
			}
		}
		if resume : stg-- : gosub *nextstage : resume=0

		// やめる
		if gvup : break
		
		//文字表示
		font "",64
		pos 0,0 : color 255,255,0
		if mode=0 : mes ""+time+"秒 "+stg+"/50回 Lv."+lv : else : mes ""+time+"秒 "+stg+"回 Lv."+lv
		
		redraw 1
		await 16
		
		//ループ回数のカウント
		if mode=0 {
			time=cnt/62
		} else {
			time=20-cnt/62
			if time<=0 : cleared=1 : break
		}
	loop
	if gvup : gvup=0 : goto *top
	if cleared : goto *gameclear
	stop

*move
	if boxx<0 | boxx>1280-200 : posx=-posx
	if boxy<0 | boxy>720-200 : posy=-posy
	boxx=boxx+posx
	boxy=boxy+posy
	return
	
*nextstage
	stg=stg+1
	if resume=0 & stg>0 & stg\10=0 & lv<5 : a=a+2 : lv++
	if mode=0 & stg=50 : cleared=1 : return
	
	boxx=rnd(1280-200)
	boxy=rnd(720-200)
	orient=rnd(8)
	if orient=0 : posx=a : posy=a
	if orient=1 : posx=-a : posy=a
	if orient=2 : posx=a : posy=-a
	if orient=3 : posx=-a : posy=-a
	if orient=4 : posx=a*3/2 : posy=0
	if orient=5 : posx=-a*3/2 : posy=0
	if orient=6 : posx=0 : posy=a*3/2
	if orient=7 : posx=0 : posy=-a*3/2
	return
	
*pause
	title "早押しゲーム "+stg+"回 "+time+"秒 Lv."+lv
	clrobj
	mmplay_opt 5

	objsize 512,128
	objimage 9,0,0
	pos 1280/2-256,320
	button gosub "",*ctn
	objimage 10,0,0
	pos 1280/2-256,320+128
	button gosub "",*giveup

	repeat
		redraw 0
		gosub *stopsc
		color 0,255,0
		font "",128,1
		pos 420,140
		mes "PAUSE"
		redraw 1
		if resume | gvup : break
		await 16
	loop

	if resume : mmplay_opt 5
	clrobj
	pos 1280-256,720-128
	objimage 8,0,0
	objsize 256,128
	button gosub "",*pause
	return

*ctn
	resume=1
	return

*giveup
	gvup=1
	return

*stopsc
	color 0,0,0
	boxf
	pos boxx,boxy
	if mode=1 : celput 1 : else : celput 1,cleared
	font "",64
	pos 0,0
	color 255,255,0
	if mode=0 : mes ""+time+"秒 "+stg+"/50回 Lv."+lv : else : mes ""+time+"秒 "+stg+"回 Lv."+lv
	return

*gameclear
	title "早押しゲーム "+stg+"回 "+time+"秒 Lv."+lv
	clrobj
	mmstop
	mmplay_opt 2

	redraw 0
	gosub *stopsc
	color 0,255,0
	font "",128,1
	pos 400,250
	mes "FINISH!!"
	redraw 1
	await 2000
*result
	cls
	cleared=0
	needsave=0
	if mode=0 {
		if besttime>time | besttime<0 {
			needsave=1
			besttime=time
			objimage 11,0,0
		} else {
			objimage 10,0,0
		}
	} else {
		if beststg<stg {
			needsave=1
			beststg=stg
			objimage 11,0,0
		} else {
			objimage 10,0,0
		}
	}
	objsize 512,128
	pos 1280/2-256,500
	button "",*saving

	repeat
		redraw 0
		color 0,0,0
		boxf

		color 255,255,0
		font "",128,1
		pos 350,50
		mes "RESULT"

		color 0,255,0
		font "",64
		pos 350,240
		if mode=0 {
			mes "かかった時間: "+time+"秒"
			mes "最高記録: "+besttime+"秒"
		} else {
			mes "回数: "+stg+"回"
			mes "最高記録: "+beststg+"回"
		}
		redraw 1
		await 16
	loop
	
*saving
	if needsave : bsave "save.bin",besttime,4 : bsave "save.bin",beststg,4,4
	goto *top

*options
	mmstop
	mmplay_opt 6
	cls

	objsize 768,128
	objimage 5,0,0
	pos 1280/2-384,220
	button "",*del_timeattack
	objimage 6,0,0
	pos 1280/2-384,220+128
	button "",*del_countdown
	objimage 7,0,0
	pos 1280/2-384,220+256
	button "",*top

	repeat
		redraw 0
			pos 0,0
			color 0,0,0
			boxf
			pos 120,50
			font "",64
			color 255,255,0
			print "初期化すると復元できません！！"
		redraw 1
		await 16
	loop

*del_after
	exist "save.bin"
	if strsize!-1 : bsave "save.bin",besttime,4 : bsave "save.bin",beststg,4,4
	cls

	redraw 0
		pos 0,0 : color 0,0,0 : boxf
		pos 120,80 : font "",64
		color 255,255,0
		mes "削除しました。"
	redraw 1
	await 1000
	goto *top

*del_timeattack
	besttime=-1
	goto *del_after

*del_countdown
	beststg=-1
	goto *del_after

// 音声ファイルが存在する場合のみ mmload する
#deffunc mmload_opt str mmload_opt_file, int mmload_opt_id, int mmload_opt_mode
	exist mmload_opt_file
	if strsize!-1 : mmlist(mmload_opt_id)=1 : mmload mmload_opt_file, mmload_opt_id, mmload_opt_mode
	return

// mmload_opt で読み込めていた場合のみ mmplay する
#deffunc mmplay_opt int mmplay_opt_id
	if mmlist(mmplay_opt_id) : mmplay mmplay_opt_id
	return
